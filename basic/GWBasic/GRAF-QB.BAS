DECLARE FUNCTION F1(X,Z)
DECLARE FUNCTION F2(X,Z)
DECLARE FUNCTION F3(X,Z)
DECLARE FUNCTION F4(X,Z)
DECLARE FUNCTION F5(X,Z)

REM * * * DECLARE GLOBAL VARIABLES * * *
DIM SHARED LARGURA, ALTURA, VA, XFAL, ZFAL, TAMANHO, PROF, INICIO, TIPO

REM * * * INICIALIZAR VARIAVEIS * * * 
LET LARGURA = 330
LET ALTURA = 200
LET VA = -1
LET XFAL = 5
LET ZFAL = 2
LET TAMANHO = INT(LARGURA / XFAL / 2)
LET PROF = INT(ALTURA / ZFAL / 3)
LET INICIO = 5
LET T2 = TAMANHO / 2
LET P2 = PROF / 2
LET T18 = 18 / TAMANHO
LET P18 = 18 / PROF
LET VALT = VA * ALTURA

DIM SHARED PT(TAMANHO, PROF)

REM *** PROGRAMA PRINCIPAL ***

CLS
FOR TIPO = 1 TO 5
	GOSUB CALCULA
	GOSUB DESENHA
	INPUT "PRESS ENTER...", TECLA$
NEXT TIPO
END

REM ---------------------------------------------
CALCULA:
	REM CALCULAR VALORES
	PRINT " CALCULANDO ... ", TIPO

	FOR A = -P2 TO P2
		FOR B = -T2 TO T2
			LET X = A * T18
			LET Z = B * P18

			REM *** SELECTIONA FUNCAO GRAFICA ***
			IF TIPO = 1 THEN Y = F1(X, Z)
			IF TIPO = 2 THEN Y = F2(X, Z)
			IF TIPO = 3 THEN Y = F3(X, Z)
			IF TIPO = 4 THEN Y = F4(X, Z)
			IF TIPO = 5 THEN Y = F5(X, Z)

			LET I = B + T2
			LET J = A + P2

			PT(I, J) = Y * VALT
		NEXT B
	NEXT A
RETURN

REM ---------------------------------------------
DESENHA:
	CLS
	SCREEN 13

	REM * * * PLANO X-Y * * *
	FOR Z = 1 TO PROF
		X = 1
		XBASE = XFAL * Z
		ZBASE = (ALTURA / 2) + (Z * ZFAL) + (INICIO * VA)
		XVELHO = XBASE + XFAL
		ZVELHO = 191 - ZBASE + (X * ZFAL) + PT(X, Z)
		FOR X = 1 TO TAMANHO
			XNOVO = XBASE + (X * XFAL)
			ZNOVO = 191 - ZBASE + (X * ZFAL) + PT(X, Z)
			
			LINE (XVELHO, ZVELHO)-(XNOVO, ZNOVO), 1
			
			XVELHO = XNOVO
			ZVELHO = ZNOVO
		NEXT X
	NEXT Z

	REM * * * PLANO Z-Y * * * 
	FOR X = 1 TO TAMANHO
			XBASE = (XFAL * X) + (PROF * XFAL)
			ZBASE = (ALTURA / 2) - (X * ZFAL) + (PROF * ZFAL) + (INICIO * VA)
			XVELHO = XBASE - XFAL
			ZVELHO = 191 - ZBASE + ZFAL + PT(X, PROF - 1)
			FOR Z = 0 TO PROF - 1
				XNOVO = XBASE - (Z * XFAL)
				ZNOVO = 191 - ZBASE + (Z * ZFAL) + PT(X, PROF - Z)
				
				LINE (XVELHO, ZVELHO)-(XNOVO, ZNOVO), 2
				
				XVELHO = XNOVO
				ZVELHO = ZNOVO
			NEXT Z
	NEXT X
RETURN

END

REM ---------------------------------------------
REM *** DECLARE FUNCTIONS ***

FUNCTION F1 (X, Z)
	F1 = (SIN(X) + COS(Z)) / 60
END FUNCTION

FUNCTION F2 (X, Z)
	F2 = SIN(X + Z) / 24
END FUNCTION

FUNCTION F3 (X, Z)
	DIM C
	C = 50 - (X * X) - (Z * Z)
	F3 = SQR(C * (SGN(C) + 1)) / 30
END FUNCTION

FUNCTION F4 (X, Z)
	DIM C
	C = (X * X) + (Z * Z) + .00001
	F4 = SGN(INT(13 / C)) / 3 + SGN(INT(35 / C)) / 15
END FUNCTION

FUNCTION F5 (X, Z)
	DIM C, D
	D = ABS(X + (Z * Z))
	C = SQR(D)
	F5 = COS(16 * C) * (1 - C) * 2
END FUNCTION
